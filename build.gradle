plugins {
    id 'java'
    id 'jacoco'
    id 'jacoco-report-aggregation'
}

configure(rootProject) {
    dependencies {
        jacocoAggregation rootProject.subprojects
    }
}

group = 'com.example'
version = '1.0-SNAPSHOT'
sourceCompatibility = 11

repositories {
    mavenCentral()
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'

    dependencies {
        implementation group: 'commons-cli', name: 'commons-cli', version: '1.5.0'
        implementation group: 'commons-io', name: 'commons-io', version: '2.11.0'

        implementation group: 'com.google.guava', name: 'guava', version: '32.1.2-jre'

        implementation 'org.eclipse.collections:eclipse-collections-api:11.1.0'
        implementation 'org.eclipse.collections:eclipse-collections:11.1.0'

        implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.20.0'

        implementation group: 'it.unimi.dsi', name: 'fastutil', version: '8.2.2'

        implementation group: 'com.koloboke', name: 'koloboke-api-jdk8', version: '1.0.0'

        testImplementation platform('org.junit:junit-bom:5.9.1')
        testImplementation 'org.junit.jupiter:junit-jupiter'

        testImplementation group: 'junit', name: 'junit', version: '4.13.1'

        testImplementation group: 'org.hamcrest', name: 'hamcrest-core', version: '2.2'
        testImplementation group: 'org.hamcrest', name: 'hamcrest', version: '2.2'

        testImplementation group: 'org.mockito', name: 'mockito-core', version: '5.5.0'

    }

    test {
        useJUnitPlatform()
        reports {
            junitXml.mergeReruns = true
        }

        jacoco {
            includes = ["**"]
        }
    }

    jacocoTestReport {
        dependsOn test // tests are required to run before generating the report
        reports {
            xml.required = true
            csv.required = false
            html.required = false
        }
    }

    jacoco {
        toolVersion = "0.8.5"
    }

}

test.finalizedBy jacocoTestReport

task uploadCoverage(type:Exec, dependsOn: testCodeCoverageReport) {
    description 'Uploads coverage data to Codacy.'
    commandLine 'bash', '-c', 'bash <(curl -Ls https://coverage.codacy.com/get.sh) report -r build/reports/jacoco/testCodeCoverageReport/testCodeCoverageReport.xml'
}
